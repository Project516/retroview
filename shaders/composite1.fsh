#version 330 compatibility

uniform sampler2D colortex0;
uniform sampler2D depthtex0;

uniform mat4 gbufferProjectionInverse;
uniform vec3 fogColor;
uniform float far;
uniform float retroPixelScale = 5.0;

in vec2 texcoord;

const float FOG_DENSITY = 5.0;

vec3 projectAndDivide(mat4 projectionMatrix, vec3 position) {
    vec4 homPos = projectionMatrix * vec4(position, 1.0);
    return homPos.xyz / homPos.w;
}

/* RENDERTARGETS: 0 */
layout(location = 0) out vec4 color;

void main() {
    vec2 screenSize = vec2(textureSize(colortex0, 0));
    vec2 lowUV = floor(texcoord * screenSize / retroPixelScale) * retroPixelScale / screenSize;

    color = texture(colortex0, texcoord);

    float depth = texture(depthtex0, lowUV).r;
    if (depth == 1.0) {
        return;
    }

    vec3 ndcPos = vec3(lowUV.xy, depth) * 2.0 - 1.0;
    vec3 viewPos = projectAndDivide(gbufferProjectionInverse, ndcPos);

    float dist = length(viewPos) / far;
    float fogFactor = exp(-FOG_DENSITY * (1.0 - dist));

    color.rgb = mix(color.rgb, pow(fogColor, vec3(2.2)), clamp(fogFactor, 0.0, 1.0));
}